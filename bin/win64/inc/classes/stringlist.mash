////////////////////////////////////////////////////////////////////////////////
// Base types
////////////////////////////////////////////////////////////////////////////////

uses strings
uses classes.list
uses classes.streams

class StringList(List):
  public:
    proc SetText(text):
      $Clear()
      while text.len() > 0:
        p ?= text.SubPos(LnBreak)

        if p <> null:
          $add(text.SubStr(0, p))
          text.Cut(0, p + LnBreak.len())
        else:
          if text.len() > 0:
            $add(text)
          end
          break
        end
      end
    end

    func ToString():
      r ?= ""
      i ?= 0
      while i < $size:
        r += $Items[i]
        if i + 1 < $size:
          r += LnBreak
        end

        i++
      end
      return r
    end

    proc SaveToStream(st):
      st -> WriteStr($ToString())
    end

    proc SaveToFile(fpath):
      st ?= new FileStream(fpath, fmCreate)
      st -> WriteStr($ToString())
      st -> Close()
    end

    proc LoadFromStream(st):
      $SetText(st -> ReadText())
    end

    proc LoadFromFile(fpath):
      st ?= new FileStream(fpath, fmOpenRead)
      $LoadFromStream(st)
      st -> Close()
    end
end
